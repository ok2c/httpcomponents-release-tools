/*
 * ====================================================================
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

package com.github.ok2.hc.release

import com.github.ok2.hc.release.pom.Pom
import com.github.ok2.hc.release.pom.PomModule
import org.ccil.cowan.tagsoup.AttributesImpl
import org.ccil.cowan.tagsoup.Parser
import org.ccil.cowan.tagsoup.XMLWriter
import org.gradle.util.GFileUtils
import org.xml.sax.Attributes
import org.xml.sax.InputSource
import org.xml.sax.SAXException

class WebsiteTool {

    private final File websiteDir
    private final File stagingDir

    WebsiteTool(File websiteDir, File stagingDir) {
        this.websiteDir = websiteDir
        this.stagingDir = stagingDir
    }

    File getWebsiteDir() {
        return websiteDir
    }

    File getStagingDir() {
        return stagingDir
    }

    void rewriteLinks(File file, String charset, Closure<URI> c) {
        File tmp = File.createTempFile("grrrr", file.name)
        try {
            Writer writer = tmp.newWriter(charset)
            try {
                Reader reader = file.newReader(charset)
                try {
                    rewriteLinks(reader, writer, c)
                } finally {
                    reader.close()
                }
            } finally {
                writer.close()
            }
            file.delete()
            GFileUtils.moveFile(tmp, file)
        } finally {
            GFileUtils.deleteQuietly(tmp)
        }
    }

    void rewriteLinks(File file, Closure<URI> c) {
        rewriteLinks(file, 'UTF-8', c)
    }

    void rewriteLinks(Reader src, Writer dst, Closure<URI> c) {
        Parser parser = new Parser()
        parser.setContentHandler(new XMLWriter(dst) {

            @Override
            void startElement(
                    String uri,
                    String localName,
                    String qName,
                    Attributes atts) throws SAXException {
                AttributesImpl newatts = null;
                int idx = -1;
                switch (localName) {
                    case 'a':
                        idx = atts.getIndex('href')
                        break;
                    case 'img':
                        idx = atts.getIndex('src')
                        break;
                }
                if (idx != -1) {
                    newatts = new AttributesImpl(atts)
                    try {
                        URI origURI = new URI(atts.getValue(idx))
                        URI newURI = c.call(origURI, localName)
                        newatts.setValue(idx, newURI.toASCIIString())
                    } catch (URISyntaxException ignore) {
                    }
                }
                super.startElement(uri, localName, qName, newatts != null ? newatts :atts)
            }
        })
        parser.parse(new InputSource(src))
    }

    void fixLinks(Pom pom, File dstDir) {
        // Deal with crappy links generated by Maven Site Plugin
        GFileUtils.listFiles(dstDir, ['html'] as String[], false).each {
            File f ->
                rewriteLinks(f, { URI href, String localName ->
                    if (!href.isAbsolute()) {
                        def m1 = href.path =~ /^..(\/..\/scp:\/people.apache.org\/www)?\/hc.apache.org\//
                        if (m1.find()) {
                            return new URI(m1.replaceFirst('../'))
                        }
                    }
                    return href
                })
        }
        pom.modules.each { PomModule submodule ->

            GFileUtils.listFiles(new File(dstDir, submodule.name), ['html'] as String[], false).each {
                File f ->
                    rewriteLinks(f, { URI href, String localName ->
                        if (!href.isAbsolute()) {
                            def m1 = href.path =~ /^..\/..(\/..\/scp:\/people.apache.org\/www)?\/hc.apache.org\//
                            if (m1.find()) {
                                return new URI(m1.replaceFirst('../../'))
                            }
                            if (href.path == '../images/logos/httpcomponents.png') {
                                return new URI('../../images/logos/httpcomponents.png')
                            }
                        }
                        return href
                    })
            }
        }

    }

    void rewriteSiteCss(Pom pom, File dstDir) {
        File siteCss = new File(dstDir, 'css/site.css')
        if (siteCss.exists()) {
            siteCss.withWriter { Writer w ->
                w  << '@import url("../../css/hc-maven.css");'
            }
        }
        pom.modules.each { PomModule submodule ->
            File moduleDstDir = new File(dstDir, submodule.name)
            File moduleSiteCss = new File(moduleDstDir, 'css/site.css')
            if (moduleSiteCss.exists()) {
                moduleSiteCss.withWriter { Writer w ->
                    w  << '@import url("../../../css/hc-maven.css");'
                }
            }
        }

    }

}
